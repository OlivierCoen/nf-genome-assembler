include { LONG_READ_PREPARATION                                              } from '../long_read_preparation/main'
include { LONG_READ_PREPARATION as HAPLOTYPE_LONG_READ_PREPARATION           } from '../long_read_preparation'

include { DRAFT_ASSEMBLY                                                     } from '../draft_assembly/main'
include { DRAFT_ASSEMBLY as HAPLOTYPE_DRAFT_ASSEMBLY                         } from '../draft_assembly/main'

include { POLISH                                                             } from '../polish/main'

include { HAPLOTYPE_PHASING                                                  } from '../haplotype_phasing'
include { PURGE_DUPLICATES                                                   } from '../purge_duplicates'


/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    CRITERIA
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

/*
def runHaplotypeCleaningCriteria = branchCriteria {
    meta, assembly ->
        to_clean: meta.clean_haplotypes
        leave_me_alone: !meta.clean_haplotypes
}
*/

def polishBranchCriteria = branchCriteria { meta, assembly ->
    polish_me: meta.polish_draft_assembly
    leave_me_alone: !meta.polish_draft_assembly
}


/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

def runAssembly = { meta, assembly -> meta.run_step.assembly }

def runHaplotypePhasing = { meta, assembly -> meta.run_step.haplotype_phasing }

def runHaplotypeAssembly = { meta, haplotype_reads -> meta.run_step.haplotype_assembly }

def runHaplotypeCleaning = { meta, haplotype -> meta.clean_haplotypes }

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    SUBWORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/


workflow MANUAL_ASSEMBLY {

    take:
    ch_input_reads
    ch_input_draft_assemblies
    ch_input_haplotype_1_reads
    ch_input_haplotype_2_reads
    ch_input_haplotypes_1
    ch_input_haplotypes_2
    ch_input_hic_reads

    main:

    ch_versions = channel.empty()
    ch_haplotypes = channel.empty()

    // ------------------------------------------------------------------------------------
    // READ PREPARATION
    // ------------------------------------------------------------------------------------

    // by default, we prepare all reads, even for samples for which we do not want an assembly
    // because reads are used at multiple different crucial steps
    LONG_READ_PREPARATION ( ch_input_reads )

    ch_prepared_reads = LONG_READ_PREPARATION.out.prepared_reads

    // --------------------------------------------------------
    // PRIMARY ASSEMBLY
    // --------------------------------------------------------

    ch_prepared_reads
        .filter ( runAssembly )
        .set { ch_prepared_reads_to_assemble }

    DRAFT_ASSEMBLY ( ch_prepared_reads_to_assemble )

    DRAFT_ASSEMBLY.out.assemblies
        .mix ( ch_input_draft_assemblies )
        .set { ch_draft_assemblies }

    // --------------------------------------------------------
    // POLISHING
    // --------------------------------------------------------

    ch_draft_assemblies
        .branch ( polishBranchCriteria )
        .set { ch_branched_draft_assemblies }

    POLISH (
        ch_prepared_reads,
        ch_branched_draft_assemblies.polish_me
    )

    // collecting all final polished draft assemblies
    ch_branched_draft_assemblies.leave_me_alone
        .mix ( POLISH.out.assemblies )
        .set { ch_draft_assemblies }

    // collecting all intermediate and final assemblies (for QC)
    ch_branched_draft_assemblies.leave_me_alone
        .mix ( POLISH.out.polished_assembly_versions )
        .mix ( DRAFT_ASSEMBLY.out.alternate_assemblies )
        .set { ch_all_draft_assembly_versions_and_alternatives }

    ch_versions = ch_versions
        .mix ( LONG_READ_PREPARATION.out.versions )
        .mix ( DRAFT_ASSEMBLY.out.versions )


    if ( params.assembly_mode == "diploid" ) {

        PURGE_DUPLICATES (
            ch_prepared_reads,
            ch_draft_assemblies
        )

        PURGE_DUPLICATES.out.purged_assemblies.set { ch_assemblies }

        ch_all_draft_assembly_versions_and_alternatives
            .mix ( PURGE_DUPLICATES.out.purged_assemblies )
            .set { ch_all_draft_assembly_versions_and_alternatives }

        ch_versions = ch_versions
                        .mix ( PURGE_DUPLICATES.out.versions )

    } else { // haplotype

        // ------------------------------------------------------------------------------------
        // HAPLOTYPE PHASING
        // ------------------------------------------------------------------------------------

        ch_draft_assemblies
            .filter ( runHaplotypePhasing )
            .set { ch_all_draft_assemblies_to_phase }

        HAPLOTYPE_PHASING (
            ch_prepared_reads,
            ch_all_draft_assemblies_to_phase
        )

        HAPLOTYPE_PHASING.out.haplotype_reads
            .mix ( ch_input_haplotype_1_reads )
            .mix ( ch_input_haplotype_2_reads )
            .set { ch_haplotype_reads }

        // ------------------------------------------------------------------------------------
        // HAPLOTYPE READ PREPARATION
        // ------------------------------------------------------------------------------------

        HAPLOTYPE_LONG_READ_PREPARATION ( ch_haplotype_reads )

        HAPLOTYPE_LONG_READ_PREPARATION.out.prepared_reads
            .set { ch_prepared_haplotype_reads }

        // --------------------------------------------------------
        // PRIMARY ASSEMBLY
        // --------------------------------------------------------

        HAPLOTYPE_DRAFT_ASSEMBLY ( ch_prepared_haplotype_reads )

        HAPLOTYPE_DRAFT_ASSEMBLY.out.assemblies
            .mix ( ch_input_haplotypes_1 )
            .mix ( ch_input_haplotypes_2 )
            .set { ch_assemblies }

        ch_all_draft_assembly_versions_and_alternatives
            .mix ( ch_assemblies )
            .mix ( HAPLOTYPE_DRAFT_ASSEMBLY.out.draft_assembly_versions )
            .set { ch_all_draft_assembly_versions_and_alternatives }

        // TODO: put polishing here

        ch_versions = ch_versions
            .mix ( HAPLOTYPE_PHASING.out.versions )
            .mix ( HAPLOTYPE_DRAFT_ASSEMBLY.out.versions )
            .mix ( HAPLOTYPE_LONG_READ_PREPARATION.out.versions )

    }

    emit:
    assemblies                                     = ch_assemblies
    all_draft_assembly_versions_and_alternatives   = ch_all_draft_assembly_versions_and_alternatives
    reads                                          = ch_prepared_reads
    haplotypes                                     = ch_haplotypes

    versions                                       = ch_versions                     // channel: [ versions.yml ]

}
